DATABASE logix

SCREEN
{
------------------------------------------------------------------------------
 [a0]       DIGITACAO DE PEDIDOS ON-LINE - MESTRE            Pedido: [a1    ]

      Carteira: [h ]
       Cliente: [a5             ]  [a6                                   ]
      Endereco: [a7                       ] [a8            ] [a9] [a10      ]
        C.G.C.: [a27                ]  ins.est.: [a28             ]
      Operacao: [a2  ]  Data Emissao: [a3        ]  Prz Entrega: [a4        ] 
Pedido Cliente: [a11                      ]      Pedido Repres.: [a12       ]
 Representante: [a13 ]     Comissao: [b]   %: [a14  ]    Repr. Adic: [a15 ]
   Lista Preco: [a16  ]               Tipo Preco: [c] [a25       ]
Desc. Adicion.: [a17  ]            Desc. Financ.: [a18  ]
 Condicao Pgto: [a19] [a20                  ]

    Tipo Frete: [d] [a26       ]         Entrega: [e]               
Transportadora: [a21            ]  [nt                   ]  
 Consignatario: [a22            ]      Finalidade: [f]                  
         Moeda: [a23] [a24        ]    Embalagem: [g]  Texto Pedido: [i]
}
END

TABLES
pedido_dig_mest,
clientes,
cidades, 
cond_pgto,
moeda 
END

ATTRIBUTES
a0  = pedido_dig_mest.cod_empresa,      NOENTRY;
a1  = pedido_dig_mest.num_pedido,       UPSHIFT ;
a2  = pedido_dig_mest.cod_nat_oper,     UPSHIFT ;
a3  = pedido_dig_mest.dat_emis_repres,  UPSHIFT;
a4  = pedido_dig_mest.dat_prazo_entrega,  UPSHIFT;
a5  = pedido_dig_mest.cod_cliente,        UPSHIFT ;
a6  = clientes.nom_cliente,             NOENTRY; 
a7  = clientes.end_cliente,             NOENTRY;
a8  = cidades.den_cidade,               NOENTRY;
a9  = cidades.cod_uni_feder,            NOENTRY;
a10 = clientes.cod_cep,                 NOENTRY;
a27 = clientes.num_cgc_cpf,             NOENTRY;
A28 = clientes.ins_estadual,            NOENTRY;
a11 = pedido_dig_mest.num_pedido_cli,   UPSHIFT;
a12 = pedido_dig_mest.num_pedido_repres,  UPSHIFT;
a13 = pedido_dig_mest.cod_repres,       UPSHIFT;
b   = pedido_dig_mest.ies_comissao,     UPSHIFT,
      INCLUDE = ("S","N"),
      COMMENTS = "Sim     Nao";
a14 = pedido_dig_mest.pct_comissao,     UPSHIFT;
a15 = pedido_dig_mest.cod_repres_adic,  UPSHIFT;
a16 = pedido_dig_mest.num_list_preco,   UPSHIFT;
c   = pedido_dig_mest.ies_preco,        UPSHIFT,
      INCLUDE = ("F", "R"),
      COMMENTS = "F=Preco Firme     R=Preco Reajustavel";
a25 = formonly.den_ies_preco,           NOENTRY;
a17 = pedido_dig_mest.pct_desc_adic,    UPSHIFT,
      COMMENTS = "Ctrl-Z para informar mais de um % desconto adicional";
a18 = pedido_dig_mest.pct_desc_financ,  UPSHIFT;
a19 = pedido_dig_mest.cod_cnd_pgto,     UPSHIFT;
a20 = cond_pgto.den_cnd_pgto,           NOENTRY;
d   = pedido_dig_mest.ies_frete,        UPSHIFT ,
      INCLUDE = (1, 2, 3, 4, 5),
      COMMENTS = "1=CIF Pago  2=CIF Cobrado  3=FOB  4=CIF Infor. Pct.  5=CIF Infor. Unit.";
a26 = formonly.den_ies_frete,           NOENTRY;
e   = pedido_dig_mest.ies_tip_entrega,  UPSHIFT ,
      INCLUDE = (1, 2, 3),
      COMMENTS = "1=Total  2=Parcial item total  3=Parcial item parcial";
a21 = pedido_dig_mest.cod_transpor,     UPSHIFT;
nt  = FORMONLY.den_transp,              NOENTRY;
a22 = pedido_dig_mest.cod_consig,       UPSHIFT;
f   = pedido_dig_mest.ies_finalidade,   UPSHIFT ,
      INCLUDE = ("1", "2", "3"),
      COMMENTS = 
"1=Contribuinte(Industr/Comerc) 2=Nao Contribuinte 3=Contribuinte(Uso/Consumo)";
a23 = pedido_dig_mest.cod_moeda,        UPSHIFT;
a24 = moeda.den_moeda,                 NOENTRY;
g   = pedido_dig_mest.ies_embal_padrao, UPSHIFT,
       INCLUDE = ("1","2","3","4","5"),
       COMMENTS = 
"1=Padr.Int 2=Padr.Ext 3=Sem Padr. 4=Padr.Cx.Int 5=Padr.Cx.Ext 6=Padr.Pallet";
h   = pedido_dig_mest.cod_tip_carteira, UPSHIFT;
i   = formonly.ies_incl_txt type char,  UPSHIFT,
      INCLUDE = ("S", "N"),
      COMMENTS = "Incluir texto para o pedido S(Sim) ou N(Nao) ";

END 
