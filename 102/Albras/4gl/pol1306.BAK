# PROGRAMA: pol1306                                                            #
# OBJETIVO: IMPORTAÇÃO DE APONTAMENTOS DO PC_FACTORY                           #
# AUTOR...: IVO H BARBOSA             R                                        #
# DATA....: 11/08/2016                                                         #
# ALTERADO:                                                                    #
#------------------------------------------------------------------------------#
 DATABASE logix

GLOBALS
   DEFINE p_cod_empresa          LIKE empresa.cod_empresa,
          p_den_empresa          LIKE empresa.den_empresa,
          p_user                 LIKE usuario.nom_usuario,
          p_status               SMALLINT,
          p_ies_impressao        CHAR(001),
          g_ies_ambiente         CHAR(001),
          p_nom_arquivo          CHAR(100),
          p_versao               CHAR(18),
          comando                CHAR(080),
          m_comando              CHAR(080),
          p_caminho              CHAR(150),
          m_caminho              CHAR(150),
          g_tipo_sgbd            CHAR(003),
          g_id_man_apont         INTEGER,
          g_tem_critica          SMALLINT          
END GLOBALS

DEFINE    m_msg                  CHAR(150),
          m_erro                 CHAR(10),
          m_cod_empresa          CHAR(02),
          m_apont                SMALLINT,
          m_qtd_movto            DECIMAL(10,3),
          m_tip_movto            CHAR(01),
          m_terminado            CHAR(01),
          m_dtini_prod           DATE,
          m_dtfim_prod           DATE,
          m_hrini_prod           CHAR(08),
          m_hrfim_prod           CHAR(08),
          m_tip_integra          CHAR(01),
          m_integrado            INTEGER,
          m_cod_status           CHAR(01),
          m_qtd_tempo            INTEGER,
          m_dat_ini              DATE,
          m_dat_fim              DATE,
          m_dat_producao         DATE,
          m_hor_ini              CHAR(05),
          m_hor_fim              CHAR(05),
          m_qtd_hor              DECIMAL(10,2),
          m_qtd_estorno          DECIMAL(10,3),
          m_seq_reg_mestre       INTEGER,
          m_qtd_apont            DECIMAL(10,3),
          m_saldo_apont          DECIMAL(10,3),
          m_fat_conver           DECIMAL(12,5),
          m_qtd_conver           DECIMAL(15,3),
          m_tip_prod             CHAR(01),
          m_txt_resumo           CHAR(80),
          m_reaponta             DECIMAL(10,3),
          m_seq_processo         INTEGER,
          m_qtd_produzida        DECIMAL(10,3), 
          m_qtd_convertida       DECIMAL(10,3),
          m_reprocessa           SMALLINT

DEFINE    m_cod_item             LIKE item.cod_item,
          m_num_ordem            LIKE ordens.num_ordem,
          m_ies_situa            LIKE ordens.ies_situa,
          m_ies_oper_final       LIKE ord_oper.ies_oper_final,
          m_cod_operac           LIKE ord_oper.cod_operac,
          m_num_seq_operac       LIKE ord_oper.num_seq_operac,
          m_num_docum            LIKE ordens.num_docum,
          m_cod_cent_cust        LIKE ord_oper.cod_cent_cust,
          m_cod_sucata           LIKE item.cod_item,
	        m_cod_local_prod       LIKE item.cod_local_estoq,
	        m_cod_local_estoq      LIKE item.cod_local_estoq,
	        m_pes_unit             LIKE item.pes_unit,
	        m_unid_item            LIKE item.cod_unid_med,
	        m_unid_sucata          LIKE item.cod_unid_med
	        

DEFINE p_w_apont_prod   RECORD 													
   cod_empresa         char(2),                         
   cod_item            char(15), 
   num_ordem           integer, 
   num_docum           char(10), 
   cod_roteiro         char(15), 
   num_altern          dec(2,0), 
   cod_operacao        char(5), 
   num_seq_operac      dec(3,0), 
   cod_cent_trab       char(5), 
   cod_arranjo         char(5), 
   cod_equip           char(15), 
   cod_ferram          char(15), 
   num_operador        char(15), 
   num_lote            char(15), 
   hor_ini_periodo     datetime hour to minute, 
   hor_fim_periodo     datetime hour to minute, 
   cod_turno           dec(3,0), 
   qtd_boas            dec(10,3), 
   qtd_refug           dec(10,3), 
   qtd_total_horas     dec(10,2), 
   cod_local           char(10), 
   cod_local_est       char(10), 
   dat_producao        date, 
   dat_ini_prod        date, 
   dat_fim_prod        date, 
   cod_tip_movto       char(1), 
   estorno_total       char(1), 
   ies_parada          smallint, 
   ies_defeito         smallint, 
   ies_sucata          smallint, 
   ies_equip_min       char(1), 
   ies_ferram_min      char(1), 
   ies_sit_qtd         char(1), 
   ies_apontamento     char(1), 
   tex_apont           char(255), 
   num_secao_requis    char(10), 
   num_conta_ent       char(23), 
   num_conta_saida     char(23), 
   num_programa        char(8), 
   nom_usuario         char(8), 
   num_seq_registro    integer, 
   observacao          char(200), 
   cod_item_grade1     char(15), 
   cod_item_grade2     char(15), 
   cod_item_grade3     char(15), 
   cod_item_grade4     char(15), 
   cod_item_grade5     char(15), 
   qtd_refug_ant       dec(10,3), 
   qtd_boas_ant        dec(10,3), 
   tip_servico         char(1), 
   abre_transacao      smallint, 
   modo_exibicao_msg   smallint, 
   seq_reg_integra     integer, 
   endereco            integer, 
   identif_estoque     char(30), 
   sku                 char(25), 
   finaliza_operacao   char(1)
END RECORD

DEFINE l_parametro      RECORD
       cod_empresa      CHAR(02),
       num_ordem        INTEGER,
       qtd_apont        DECIMAL(10,3)
END RECORD

DEFINE m_cod_cent_trab  LIKE cent_trabalho.cod_cent_trab,
       m_den_cent_trab  LIKE cent_trabalho.den_cent_trab

DEFINE m_qtd_erro       INTEGER,
       m_corte_periodo  INTEGER

MAIN
   CALL log0180_conecta_usuario()
   WHENEVER ANY ERROR CONTINUE
   SET ISOLATION TO DIRTY READ
   SET LOCK MODE TO WAIT 30
   LET p_versao = "pol1306-12.00.03  "
   CALL func002_versao_prg(p_versao)

   LET g_tipo_sgbd = LOG_getCurrentDBType()

   CALL log001_acessa_usuario("ESPEC999","")
      RETURNING p_status, p_cod_empresa, p_user

   IF p_status = 0 THEN

      CALL pol1306_controle()

      IF m_qtd_erro > 0 THEN
         LET m_msg = 'Alguns apontamentos foram criticados\n',
                     'Consulte os erros pelo POL1308.'
         CALL log0030_mensagem(m_msg,'info')
      END IF
   END IF
   
END MAIN

#--------------------------#
FUNCTION pol1306_controle()#
#--------------------------#

   DEFINE l_nom_tela        CHAR(200)

   INITIALIZE l_nom_tela TO NULL 
   CALL log130_procura_caminho("pol1306") RETURNING l_nom_tela
   LET l_nom_tela = l_nom_tela CLIPPED 
   OPEN WINDOW w_pol1306 AT 5,10 WITH FORM l_nom_tela
      ATTRIBUTE(BORDER, MESSAGE LINE LAST, PROMPT LINE LAST)
    
   DISPLAY p_cod_empresa TO cod_empresa

   SELECT parametro_numerico
     INTO m_corte_periodo
     FROM min_par_modulo 
    WHERE empresa = p_cod_empresa 
      AND parametro = 'TOL_CORTE_FIM_PERIOD'

   SELECT cod_turno,
          hor_inicial,
          hor_final
     INTO p_w_apont_prod.cod_turno,      
          p_w_apont_prod.hor_ini_periodo,
          p_w_apont_prod.hor_fim_periodo 
     FROM man_apont_304
    WHERE id_registro = 1
    
   IF STATUS = 0 THEN
      CALL pol1306_acerta_turno(p_w_apont_prod.cod_turno) RETURNING p_status
   END if 
   
   RETURN          
          
   LET INT_FLAG = FALSE
   LET m_qtd_erro = 0
   
   INPUT m_cod_cent_trab
      WITHOUT DEFAULTS FROM cod_cent_trab
      
      AFTER FIELD cod_cent_trab    
         IF m_cod_cent_trab IS NOT NULL THEN
            SELECT den_cent_trab
              INTO m_den_cent_trab
              FROM cent_trabalho
             WHERE cod_empresa = p_cod_empresa
               AND cod_cent_trab = m_cod_cent_trab
            IF STATUS <> 0 THEN
               CALL log003_err_sql('SELECT','cent_trabalho')
               NEXT FIELD cod_cent_trab
            END IF
            DISPLAY m_den_cent_trab to den_cent_trab
         END IF 

      ON KEY (control-z)
         CALL pol1306_popup()
   
   END INPUT
   
   IF INT_FLAG THEN
      RETURN 
   END IF
   
   IF log004_confirm(18,35) THEN
      CALL pol1306_processa() RETURNING p_status
   END IF
   
END FUNCTION

#-----------------------#
 FUNCTION pol1306_popup()
#-----------------------#

   DEFINE p_codigo CHAR(15)

   CASE
      WHEN INFIELD(cod_cent_trab)
         CALL log009_popup(8,10,"CENTRO DE TRABALHO","cent_trabalho",
              "cod_cent_trab","den_cent_trab","","S","") 
              RETURNING p_codigo
         CALL log006_exibe_teclas("01 02 07", p_versao)
                   
         IF p_codigo IS NOT NULL THEN
            LET m_cod_cent_trab = p_codigo CLIPPED
            DISPLAY p_codigo TO cod_cent_trab
         END IF
   END CASE 

END FUNCTION 
   

#------------------------------#
FUNCTION pol1306_job(l_rotina) #
#------------------------------#

   DEFINE l_rotina          CHAR(06),
          l_den_empresa     CHAR(50),
          l_param1_empresa  CHAR(02),
          l_param2_user     CHAR(08),
          l_param3_user     CHAR(08),
          l_status          SMALLINT

   #CALL JOB_get_parametro_gatilho_tarefa(1,0) RETURNING l_status, l_param1_empresa
   #CALL JOB_get_parametro_gatilho_tarefa(2,0) RETURNING l_status, l_param2_user
   #CALL JOB_get_parametro_gatilho_tarefa(2,2) RETURNING l_status, l_param3_user
   
   IF l_param1_empresa IS NULL THEN
      LET l_param1_empresa = '01'
   END IF
      
   LET p_cod_empresa = l_param1_empresa
   LET p_user = l_param2_user
   
   IF p_user IS NULL THEN
      LET p_user = 'admlog'
   END IF
      
   CALL pol1306_processa() RETURNING p_status
   
   RETURN p_status
   
END FUNCTION   

#--------------------------#
FUNCTION pol1306_processa()#
#--------------------------#   
   
   DEFINE l_ret             SMALLINT
   
   LET m_msg = NULL

   IF m_cod_cent_trab IS NULL THEN
      LET m_cod_cent_trab =  ''
   END IF
   
   CALL pol1306_chama_delphi() RETURNING p_status
   
   LET m_reprocessa = FALSE
   LET g_id_man_apont = 0
   CALL pol1306_aponta()
   
   IF m_msg IS NULL THEN
      LET g_id_man_apont = 0
      CALL pol1306_estorna()
      IF m_msg IS NULL THEN
         IF m_reprocessa THEN
            CALL pol1306_aponta()
         END IF
      END IF
   END IF
   
   IF m_msg IS NULL THEN
      LET l_ret = TRUE
      LET m_msg = 'SUCESSO'
   ELSE
      IF g_id_man_apont > 0 THEN
         CALL pol1306_ins_erro() RETURNING p_status
         LET m_integrado = 3
         CALL pol1306_atu_man() RETURNING p_status
      END IF
      LET l_ret = FALSE
   END IF
   
   INSERT INTO porc_pol1306_304
    VALUES(0,CURRENT,m_msg)
      
   RETURN l_ret
   
END FUNCTION   

#------------------------------#
FUNCTION pol1306_chama_delphi()#
#------------------------------#

   DEFINE l_param         CHAR(01),
          p_comando       CHAR(200),
          l_proces        CHAR(01),
          l_carac         CHAR(01)
  
   LET p_caminho = NULL
  
   DECLARE cq_caminho CURSOR FOR
    SELECT nom_caminho
      FROM path_logix_v2
     WHERE cod_sistema = 'DPH'

   FOREACH cq_caminho INTO p_caminho
     
      IF STATUS <> 0 THEN
         LET m_msg = 'ERRO ',m_erro CLIPPED,
            ' LENDO CAMINHO DE DIRETORIO DO SISTEMA DPH.'
         RETURN FALSE
      END IF
     
      EXIT FOREACH
   END FOREACH
  
   IF p_caminho IS NULL THEN
      LET m_msg = 'Caminho do sistema DPH não en-\n',
                  'contrado. Consulte a log1100.'
      RETURN FALSE
   END IF

   SELECT id_proces
     FROM proces_export_factory
   
   IF STATUS = 100 THEN
      INSERT INTO proces_export_factory(
        id_proces, proces_export, proces_import)
       VALUES(1,'N','N')
   END IF
   
   LET l_proces = 'S'
   
   UPDATE proces_export_factory
      SET proces_import = l_proces
    WHERE id_proces = 1
   
   LET p_comando = p_caminho CLIPPED, 'pgi1310.exe ', m_cod_cent_trab #colocar nome do pgi aqui

   CALL conout(p_comando)                            #tirar o comentario

   CALL runOnClient(p_comando)                       #tirar o comentario

   DISPLAY 'Carregando apontamentos ' AT 10,14
       #lds CALL LOG_refresh_display()

   LET l_carac = '/'
   LET l_proces = 'S'
   
   WHILE l_proces = 'S'
   
      SELECT proces_import
        INTO l_proces
        FROM proces_export_factory
       WHERE id_proces = 1
   
       IF STATUS <> 0 THEN
          LET l_proces = 'N'
       END IF
   
       IF l_carac = '/' THEN
          LET l_carac = '-'
       ELSE
          LET l_carac = '/'
       END IF
       
       DISPLAY l_carac AT 10,45
       #lds CALL LOG_refresh_display()
       
       SLEEP 1
       
   END WHILE    
  
   RETURN TRUE      

END FUNCTION   

#-------------------------#
 FUNCTION pol1306_aponta()
#-------------------------#       
   
   DEFINE l_count     INTEGER
   
   IF NOT pol1306_w_parada() THEN
      RETURN
   END IF
      				
   DISPLAY 'Apontando Ordem: ' AT 12,14
    #lds CALL LOG_refresh_display()
     
   DECLARE cq_apont CURSOR WITH HOLD FOR 	
    SELECT cod_empresa,
           cod_item,
           num_ordem,
           num_docum,
           cod_operac,
           num_seq_operac,
           cod_cent_trab,
           cod_turno,
           cod_arranjo,
           cod_eqpto,
           cod_ferramenta,
           hor_inicial,
           hor_final,
           qtd_refugo,
           qtd_movto,
           qtd_hor,
           cod_local_prod,
           cod_local_est, 
           dat_inicial,
           dat_final,
           matricula,
		       ies_terminado, 
		       id_registro,
		       num_lote, 
		       cod_roteiro, 
		       num_rot_alt,
		       unid_funcional,
		       cod_status,
		       tip_integra,
		       tip_movto,
		       qtd_tempo
      FROM man_apont_304
		 WHERE integrado = 1
		   AND tip_integra <> 'E'
	   ORDER BY id_registro
			         	 
	 FOREACH cq_apont INTO 	
	    p_w_apont_prod.cod_empresa,
			p_w_apont_prod.cod_item,
			p_w_apont_prod.num_ordem,
			p_w_apont_prod.num_docum,
			p_w_apont_prod.cod_operacao ,
			p_w_apont_prod.num_seq_operac,
			p_w_apont_prod.cod_cent_trab ,
			p_w_apont_prod.cod_turno ,
			p_w_apont_prod.cod_arranjo ,
			p_w_apont_prod.cod_equip ,
			p_w_apont_prod.cod_ferram ,
			p_w_apont_prod.hor_ini_periodo,
			p_w_apont_prod.hor_fim_periodo,
			p_w_apont_prod.qtd_refug ,
			m_qtd_movto,
			p_w_apont_prod.qtd_total_horas ,
			p_w_apont_prod.cod_local ,
			p_w_apont_prod.cod_local_est ,
			p_w_apont_prod.dat_ini_prod ,
			p_w_apont_prod.dat_fim_prod ,
			p_w_apont_prod.num_operador ,
			p_w_apont_prod.finaliza_operacao,
			g_id_man_apont,
			p_w_apont_prod.num_lote,
			p_w_apont_prod.cod_roteiro,
			p_w_apont_prod.num_altern,
			p_w_apont_prod.num_secao_requis,
			m_cod_status,
			m_tip_integra,
			m_tip_movto,
			m_qtd_tempo

      IF STATUS <> 0 THEN
         LET m_erro = STATUS
         LET m_msg = 'ERRO ',m_erro CLIPPED, ' LENDO TABELA MAN_APONT_304'
         RETURN
      END IF 

      LET p_cod_empresa = p_w_apont_prod.cod_empresa
      
      LET m_msg = func006_del_erro()
      
      IF m_msg IS NOT NULL THEN
         RETURN
      END IF
      
	    LET m_msg = NULL
	    LET g_tem_critica = FALSE
      LET m_num_ordem  = p_w_apont_prod.num_ordem
      LET m_cod_operac = p_w_apont_prod.cod_operacao
      LET m_cod_item = p_w_apont_prod.cod_item

      MESSAGE ''
      ERROR ''
      
      DISPLAY m_num_ordem AT 12,33
      DISPLAY m_qtd_movto AT 12,50
       #lds CALL LOG_refresh_display()

   SELECT cod_local_prod,
          cod_local_estoq,
          num_lote		
     INTO p_w_apont_prod.cod_local,
          p_w_apont_prod.cod_local_est,	
          p_w_apont_prod.num_lote	
     FROM ordens
    WHERE cod_empresa = p_cod_empresa
      AND num_ordem = m_num_ordem

   IF STATUS <> 0 THEN
      LET m_erro = STATUS
      LET m_msg = 'ERRO ',m_erro CLIPPED, ' LENDO DADOS DA TAB ORDENS'
      RETURN 
   END IF 
      					
			IF p_w_apont_prod.cod_cent_trab IS NULL OR
			   p_w_apont_prod.cod_cent_trab = ' '   THEN 
				 LET p_w_apont_prod.cod_cent_trab = 0
			END IF 
			
			IF p_w_apont_prod.cod_arranjo = ' '   OR
			   p_w_apont_prod.cod_arranjo IS NULL THEN 
				 LET p_w_apont_prod.cod_arranjo = 0
			END IF 
			
			IF p_w_apont_prod.cod_ferram = ' '   OR  
			   p_w_apont_prod.cod_ferram IS NULL THEN 
				 INITIALIZE p_w_apont_prod.cod_ferram  TO NULL
				 LET p_w_apont_prod.ies_ferram_min =  "N"
			ELSE 
					LET p_w_apont_prod.ies_ferram_min =  "S"
			END IF 				
			
			IF p_w_apont_prod.cod_equip = ' '   OR 
			   p_w_apont_prod.cod_equip IS NULL THEN
				 LET p_w_apont_prod.ies_equip_min = "N"
         INITIALIZE p_w_apont_prod.cod_equip  TO NULL		 
			ELSE
				 LET p_w_apont_prod.ies_equip_min = "S"	
			END IF 
			
 	    DELETE FROM w_parada

      IF STATUS <> 0 THEN
         LET m_erro = STATUS
         LET m_msg = 'ERRO ',m_erro CLIPPED, ' LIMPANDO DADOS DA TAB W_PARADA'
         RETURN 
      END IF
    
		  IF m_tip_integra = 'T' THEN				
   	     IF NOT pol1306_acerta_turno(p_w_apont_prod.cod_turno) THEN
   	        RETURN
   	     END IF
   	     CALL pol1306_calc_hora(
   	           p_w_apont_prod.hor_ini_periodo, p_w_apont_prod.hor_fim_periodo)
         LET p_w_apont_prod.qtd_total_horas = m_qtd_hor
         UPDATE man_apont_304
            SET qtd_hor = m_qtd_hor
          WHERE cod_empresa = p_cod_empresa
            AND id_registro = g_id_man_apont

         IF STATUS <> 0 THEN
            LET m_erro = STATUS
            LET m_msg = 'ERRO ',m_erro CLIPPED, ' ATUALIZADNDO DADOS DA TAB MAN_APONT_304'
            RETURN 
         END IF
         
         IF NOT pol1306_le_paradas() THEN
            RETURN
         END IF
            
   	  ELSE
   			 LET p_w_apont_prod.qtd_total_horas = 0
      END IF
			
			IF m_tip_movto = '1' THEN
			   LET p_w_apont_prod.ies_sucata = 0
   			 LET p_w_apont_prod.qtd_boas = m_qtd_movto
			ELSE
			   LET p_w_apont_prod.ies_sucata = 1
			   LET p_w_apont_prod.qtd_boas = 0
                  
         SELECT cod_item
           INTO m_cod_sucata
           FROM item_sucata_304
          WHERE cod_empresa = p_cod_empresa
            AND cod_operac = m_cod_operac

	       IF STATUS = 100 THEN
	    	    LET m_msg = ' - ITEM SUCATA NAO CADASTRADO;'
	    	    IF NOT pol1306_ins_erro() THEN
	    	       RETURN
	    	    END IF
  	     ELSE
	          IF STATUS <> 0 THEN
    	         LET m_erro = STATUS
	    	       LET m_msg = 'ERRO: ',m_erro CLIPPED, ' LENDO TAB ITEM_SUCATA_304'
	    	       RETURN
            ELSE
               IF NOT pol1306_le_itens() THEN
                 RETURN
               END IF
               IF m_unid_item = m_unid_sucata THEN
                  LET m_fat_conver = 1
                  LET m_qtd_conver = m_qtd_movto
               ELSE
                  LET m_fat_conver = m_pes_unit
                  LET m_qtd_conver = m_qtd_movto * m_fat_conver
               END IF
                  
		 			     IF pol1306_w_sucata() THEN 
		 				      INSERT INTO w_sucata 
		 				        VALUES(m_cod_sucata, m_qtd_movto, m_fat_conver, m_qtd_conver, 11  )
    	    	      IF STATUS <> 0 THEN
    	    	         LET m_msg = 'ERRO: ',m_erro CLIPPED, ' INSERINDO NA TAB W_SUCATA'
	    	             RETURN
	    	          END IF
		 			     END IF 
	    	    END IF
	    	 END IF
	    END IF 

      IF g_tem_critica THEN
      ELSE
         IF m_tip_integra = 'A' THEN	
            LET l_parametro.cod_empresa = p_cod_empresa
            LET l_parametro.num_ordem = m_num_ordem
            LET l_parametro.qtd_apont = m_qtd_movto
            LET m_msg = func006_checa_saldo(l_parametro)
            IF m_msg IS NOT NULL THEN
               CALL pol1306_ins_erro() RETURNING p_status
               RETURN
            END IF
         END IF
      END IF
      
      LET m_cod_status = 'N'
      
      IF g_tem_critica THEN 
         LET m_integrado = 3
  	 	   IF NOT pol1306_atu_man() THEN
	 	        RETURN
	 	     END IF
	 	     CONTINUE FOREACH
	 	  END IF
         
      LET m_integrado = 1

			LET p_w_apont_prod.dat_producao	        =	p_w_apont_prod.dat_ini_prod
			LET p_w_apont_prod.estorno_total        = "N"
			LET p_w_apont_prod.cod_tip_movto        = 'N'
 		  LET p_w_apont_prod.ies_defeito          = 0
      LET p_w_apont_prod.qtd_refug            = 0			
			LET p_w_apont_prod.ies_sit_qtd 					=	'L'
			LET p_w_apont_prod.ies_apontamento 			= '1'	
			LET p_w_apont_prod.num_conta_ent				= NULL
			LET p_w_apont_prod.num_conta_saida 			= NULL
			LET p_w_apont_prod.num_programa 				= 'POL1306'
			LET p_w_apont_prod.nom_usuario 					= p_user
			LET p_w_apont_prod.cod_item_grade1 			= NULL
			LET p_w_apont_prod.cod_item_grade2 			= NULL
			LET p_w_apont_prod.cod_item_grade3 			= NULL
			LET p_w_apont_prod.cod_item_grade4 			= NULL
			LET p_w_apont_prod.cod_item_grade5 			= NULL
			LET p_w_apont_prod.qtd_refug_ant 				= NULL
			LET p_w_apont_prod.qtd_boas_ant 				= NULL
			LET p_w_apont_prod.abre_transacao 			= 1
			LET p_w_apont_prod.modo_exibicao_msg 		= 0
			LET p_w_apont_prod.seq_reg_integra 			= NULL
			LET p_w_apont_prod.endereco 						= ' '
			LET p_w_apont_prod.identif_estoque 			= ' '
			LET p_w_apont_prod.sku 									= ' ' 
			
			SELECT COUNT(*) INTO l_count  FROM w_parada
			
			IF l_count > 0 THEN
			   LET p_w_apont_prod.ies_parada = 1
			ELSE
			   LET p_w_apont_prod.ies_parada = 0
			END IF      

      IF NOT pol1306_le_ord_oper() THEN
         RETURN 
      END IF

      LET m_integrado = 3	
      LET m_cod_status = 'C'
      
      IF manr24_cria_w_comp_baixa (0) THEN
      END IF
      
	 	  IF manr24_cria_w_apont_prod(0)  THEN 

	 		   CALL man8246_cria_temp_fifo()
	 		   CALL man8237_cria_tables_man8237()
        								
	 		   IF manr24_inclui_w_apont_prod(p_w_apont_prod.*,1) THEN # incluindo apontamento

 	 			    IF p_w_apont_prod.ies_defeito = 1  THEN             #apontando defeitos
		 			     IF pol1306_w_defeito() THEN 
		 				      INSERT INTO w_defeito 
		 				        VALUES(0,p_w_apont_prod.qtd_refug)
		 			     END IF 
		 		    END IF 
	 				 				  
	 			    IF manr27_processa_apontamento()  THEN #processando apontamento
	 			       LET m_integrado = 2	 
	 			       LET m_cod_status = 'A'
	 			    END IF 
	 	     ELSE
	 	        LET m_txt_resumo = 'ERRO:',STATUS,'INCLUINDO TAB W_APONT_PROD'
	 	        INSERT INTO man_log_apo_prod(empresa,ordem_producao,texto_resumo)
	 	         VALUES(p_cod_empresa, m_num_ordem, m_txt_resumo) 
	 	     END IF 
	 	  ELSE
	 	     LET m_txt_resumo = 'ERRO:',STATUS,'INCLUINDO TAB W_APONT_PROD'
	 	     INSERT INTO man_log_apo_prod(empresa,ordem_producao,texto_resumo)
	 	       VALUES(p_cod_empresa, m_num_ordem, m_txt_resumo) 
	 	  END IF

      IF m_integrado = 3 THEN
         IF NOT pol1306_le_erros() THEN
	 			    RETURN
	 			 END IF			     
	 	  END IF
	 	  
	 	  DELETE FROM w_apont_prod 
	 	  
	 	  IF NOT pol1306_atu_man() THEN
	 	     RETURN
	 	  END IF
	 	             	
   END FOREACH
    
END FUNCTION

#--------------------------#
FUNCTION pol1306_le_itens()#
#--------------------------#

   SELECT pes_unit,
          cod_unid_med
     INTO m_pes_unit, m_unid_item
     FROM item
	  WHERE cod_empresa = p_cod_empresa
	    AND cod_item = m_cod_item
	        
   IF STATUS <> 0 THEN
      LET m_erro = STATUS
	    LET m_msg = 'ERRO: ',m_erro CLIPPED, ' LENDO TAB ITEM:PESO'
	    RETURN FALSE
	 END IF

   SELECT cod_unid_med
     INTO m_unid_sucata
     FROM item
	  WHERE cod_empresa = p_cod_empresa
	    AND cod_item = m_cod_sucata
	        
   IF STATUS <> 0 THEN
      LET m_erro = STATUS
	    LET m_msg = 'ERRO: ',m_erro CLIPPED, ' LENDO TAB ITEM:SUCATA'
	    RETURN FALSE
	 END IF
	 
   RETURN TRUE

END FUNCTION
   
#----------------------------#
 FUNCTION pol1306_w_defeito()#
#----------------------------#
	
	DROP TABLE w_defeito

	CREATE TEMP TABLE w_defeito(
				cod_defeito		DECIMAL(3,0),
				qtd_refugo		DECIMAL(10,3)
		)

	IF STATUS <> 0 THEN
		RETURN FALSE
	ELSE 
		RETURN TRUE
	END IF 

END FUNCTION 

#---------------------------#
 FUNCTION pol1306_w_sucata()
#---------------------------#
	
	DROP TABLE w_sucata

  CREATE TEMP TABLE w_sucata	(	
     cod_sucata      	CHAR(15),
     qtd_apont	        DECIMAL(15,3),
     fat_conversao	    DECIMAL(12,5),
     qtd_convertida  	DECIMAL(15,3),
     motivo_sucata 	  DECIMAL(3,0)
   );	

   IF STATUS <> 0 THEN
      LET m_erro = STATUS
      LET m_msg = 'ERRO ',m_erro CLIPPED, ' CRIANDO TABELA W_SUCATA '
      RETURN FALSE
   END IF 

   RETURN TRUE

END FUNCTION 
			
#-------------------------#
FUNCTION pol1306_atu_man()#
#-------------------------#

   UPDATE man_apont_304
      SET integrado = m_integrado,
          den_erro = m_msg,
          cod_status = m_cod_status
    WHERE id_registro = g_id_man_apont
      AND cod_empresa = p_cod_empresa

   IF STATUS <> 0 THEN
      LET m_erro = STATUS
      LET m_msg = 'ERRO ',m_erro CLIPPED, ' ATUALIZANDO TAB MAN_APONT_304 '
      RETURN FALSE
   END IF 

   RETURN TRUE

END FUNCTION

#--------------------------#
FUNCTION pol1306_le_erros()#
#--------------------------#
   
   DEFINE l_erro  CHAR(500)
   
   LET m_msg = ''
   
   DECLARE cq_erro CURSOR FOR 	
		SELECT texto_resumo  	
		 	FROM man_log_apo_prod	
     WHERE empresa = p_cod_empresa
       AND ordem_producao = m_num_ordem
		  
   FOREACH cq_erro INTO l_erro	
  				
      IF STATUS <> 0 THEN
         LET m_erro = STATUS
         LET m_msg = 'ERRO ',m_erro CLIPPED, ' LENDO ERROS DA TAB MAN_LOG_APO_PROD '
         RETURN FALSE
      END IF 
      
      LET m_msg = l_erro 

 	    IF NOT pol1306_ins_erro() THEN
 	       RETURN FALSE
 	    END IF
   
   END FOREACH
   
   LET m_msg = ''
   
   RETURN TRUE

END FUNCTION

#--------------------------#
FUNCTION pol1306_ins_erro()#
#--------------------------#
   
   LET g_tem_critica = TRUE
   LET m_qtd_erro = m_qtd_erro + 1
   
   INSERT INTO apont_erro_912
    VALUES(p_cod_empresa, g_id_man_apont, m_msg)

   IF STATUS <> 0 THEN
      LET m_erro = STATUS
      LET m_msg = 'ERRO ',m_erro CLIPPED, ' INSERINDO DADOS NA TAB APONT_ERRO_912'
      RETURN FALSE
   END IF 
   
   RETURN TRUE

END FUNCTION   
    
#----------------------------#
 FUNCTION pol1306_w_estorna()#
#----------------------------#
	
	DROP TABLE w_estorna_304

	CREATE TEMP TABLE w_estorna_304(
				seq_reg_mestre		INTEGER,
				qtd_apont     		DECIMAL(10,3),
		    qtd_produzida     DECIMAL(10,3),
        qtd_convertida    DECIMAL(10,3)
   )

	IF STATUS <> 0 THEN
     LET m_erro = STATUS
     LET m_msg = 'ERRO ',m_erro CLIPPED, ' CIRANDO TABELA W_ESTORNA_304'
		 RETURN FALSE
	ELSE 
     RETURN TRUE
	END IF 

END FUNCTION 

#-------------------------------#
FUNCTION pol1306_ins_w_estorno()#
#-------------------------------#

   INSERT INTO w_estorna_304
    VALUES(m_seq_reg_mestre, m_qtd_apont, m_qtd_produzida, m_qtd_convertida)

	IF STATUS <> 0 THEN
     LET m_erro = STATUS
     LET m_msg = 'ERRO ',m_erro CLIPPED, ' INSERINDO NA TABELA W_ESTORNA_304'
		 RETURN FALSE
	ELSE 
     RETURN TRUE
	END IF 

END FUNCTION
    
#-------------------------#
 FUNCTION pol1306_estorna()
#-------------------------#       

   DEFINE lr_tip_sucata RECORD
          cod_empresa       CHAR(02),
			    seq_reg_mestre    INTEGER,
			    estorno_total     CHAR(01),
			    tip_apont_sucata  CHAR(01), 
			    item_trata_qea    SMALLINT
   END RECORD
         				
   DISPLAY 'Verificando estornos: ' AT 12,14
    #lds CALL LOG_refresh_display()

   SELECT log_defn_parametro.val_padrao
     INTO lr_tip_sucata.tip_apont_sucata
     FROM log_defn_parametro 
    WHERE log_defn_parametro.parametro='tipo_apont_sucata'

   DECLARE cq_estorna CURSOR WITH HOLD FOR 	
    SELECT cod_empresa,
           cod_item,
           num_ordem,
           num_docum,
           cod_operac,
           num_seq_operac,
           cod_local_prod,
           cod_local_est, 
           qtd_movto,
		       id_registro,
		       tip_integra,
		       tip_movto,
		       dat_inicial
      FROM man_apont_304
		 WHERE integrado = 1
		   AND tip_integra = 'E'
	   ORDER BY id_registro
			         	 
	 FOREACH cq_estorna INTO 	
	    p_cod_empresa,
	    m_cod_item,
	    m_num_ordem,
	    m_num_docum,
	    m_cod_operac,
	    m_num_seq_operac,
	    m_cod_local_prod,
	    m_cod_local_estoq,
			m_qtd_movto,
			g_id_man_apont,
			m_tip_integra,
			m_tip_movto,
			m_dat_producao

      IF STATUS <> 0 THEN
         LET m_erro = STATUS
         LET m_msg = 'ERRO ',m_erro CLIPPED, ' LENDO TABELA MAN_APONT_304'
         RETURN
      END IF 

      LET m_msg = func006_del_erro()
      
      IF m_msg IS NOT NULL THEN
         RETURN
      END IF

      IF NOT pol1306_w_estorna() THEN
         RETURN
      END IF    

	    LET m_msg = NULL
      LET g_tem_critica = FALSE
      
      DISPLAY m_num_ordem AT 12,33
      DISPLAY m_qtd_movto AT 12,50
       #lds CALL LOG_refresh_display()
      								      
			
			IF m_tip_movto = '1' THEN
			ELSE
         SELECT cod_item
           INTO m_cod_sucata
           FROM item_sucata_304
          WHERE cod_empresa = p_cod_empresa
            AND cod_operac = m_cod_operac

	       IF STATUS = 100 THEN
	    	    LET m_msg = ' - ITEM SUCATA NAO CADASTRADO;'
	    	    IF NOT pol1306_ins_erro() THEN
	    	       RETURN
	    	    END IF
  	     ELSE
	          IF STATUS <> 0 THEN
    	         LET m_erro = STATUS
	    	       LET m_msg = 'ERRO: ',m_erro CLIPPED, ' LENDO TAB ITEM_SUCATA_304'
	    	       RETURN
            ELSE

               IF NOT pol1306_le_itens() THEN
                 RETURN
               END IF
               IF m_unid_item = m_unid_sucata THEN
                  LET m_fat_conver = 1
                  LET m_qtd_conver = m_qtd_movto
               ELSE
                  LET m_fat_conver = m_pes_unit
                  LET m_qtd_conver = m_qtd_movto * m_fat_conver
               END IF

		 			     IF pol1306_w_sucata() THEN 
		 				      INSERT INTO w_sucata 
		 				        VALUES(m_cod_sucata, m_qtd_movto, m_fat_conver, m_qtd_conver, 11  )
    	    	      IF STATUS <> 0 THEN
    	    	         LET m_msg = 'ERRO: ',m_erro CLIPPED, ' INSERINDO NA TAB W_SUCATA'
	    	             RETURN
	    	          END IF
		 			     END IF 
                  
               LET m_cod_item = m_cod_sucata               

	    	    END IF
	    	 END IF
	    END IF 
      
      LET m_cod_status = 'N'
      
      IF g_tem_critica THEN 
         LET m_integrado = 3
  	 	   IF NOT pol1306_atu_man() THEN
	 	        RETURN
	 	     END IF
	 	     CONTINUE FOREACH
	 	  END IF
      
      LET m_saldo_apont = 0
      
      IF m_tip_movto = '1' THEN
         LET m_tip_prod = 'B'
      ELSE
         LET m_tip_prod = 'S'
      END IF
      
      DECLARE cq_mestre CURSOR FOR                                               
       SELECT man_apo_mestre.seq_reg_mestre,   
              man_item_produzido.qtd_produzida,                      
              man_item_produzido.qtd_convertida                                      
         FROM man_apo_mestre,                                                       
              man_item_produzido,                                                   
              man_apo_detalhe                                                       
        WHERE man_apo_mestre.empresa = p_cod_empresa                                
          AND man_apo_mestre.tip_moviment = 'N'                                     
          AND man_apo_mestre.ordem_producao = m_num_ordem                           
          AND man_apo_mestre.data_producao = m_dat_producao                         
          AND man_item_produzido.empresa = man_apo_mestre.empresa                   
          AND man_item_produzido.seq_reg_mestre = man_apo_mestre.seq_reg_mestre     
          AND man_item_produzido.tip_movto = 'N'                                    
          AND man_item_produzido.tip_producao = m_tip_prod                          
          AND man_item_produzido.item_produzido = m_cod_item                        
          AND man_apo_detalhe.empresa = man_apo_mestre.empresa                      
          AND man_apo_detalhe.seq_reg_mestre = man_apo_mestre.seq_reg_mestre        
          AND man_apo_detalhe.operacao = m_cod_operac                               
          AND man_apo_detalhe.sequencia_operacao = m_num_seq_operac                 
        ORDER BY man_apo_mestre.seq_reg_mestre
        
      FOREACH cq_mestre INTO m_seq_reg_mestre, m_qtd_produzida, m_qtd_convertida 
      
         IF STATUS <> 0 THEN
            LET m_erro = STATUS
            LET m_msg = 'ERRO ',m_erro CLIPPED, ' LENDO TABELAS DE APONTAMENTO'
            RETURN
         END IF 
         
         IF m_tip_movto = '1' THEN
            LET m_qtd_apont = m_qtd_produzida
         ELSE
            LET m_qtd_apont = m_qtd_convertida
         END IF
         
         SELECT SUM(qtd_produzida)
                INTO m_qtd_estorno
           FROM man_item_produzido
          WHERE empresa = p_cod_empresa
            AND seq_reg_mestre = m_seq_reg_mestre
            AND tip_movto = 'E'

         IF STATUS <> 0 THEN
            LET m_erro = STATUS
            LET m_msg = 'ERRO ',m_erro CLIPPED, ' LENDO TABELAS DE MAN_ITEM_PRODUZIDO'
            RETURN
         END IF 
         
         IF m_qtd_estorno IS NULL THEN
            LET m_qtd_estorno = 0
         END IF
         
         LET m_qtd_apont = m_qtd_apont - m_qtd_estorno
         
         IF m_qtd_apont <= 0 THEN 
            CONTINUE FOREACH
         END IF
                     
         IF NOT pol1306_ins_w_estorno() THEN
            RETURN
         END IF
         
         LET m_saldo_apont = m_saldo_apont + m_qtd_apont
         
         IF m_saldo_apont >= m_qtd_movto THEN
            EXIT FOREACH
         END IF
         
      END FOREACH
      
      FREE cq_mestre
      
      SELECT SUM(qtd_apont)
        INTO m_saldo_apont
        FROM w_estorna_304

      IF STATUS <> 0 THEN
         LET m_erro = STATUS
         LET m_msg = 'ERRO ',m_erro CLIPPED, ' SUMARIZANDO TABELA DE W_ESTORNA_304'
         RETURN
      END IF 
      
      IF m_saldo_apont IS NULL THEN
         LET m_saldo_apont = 0
      END IF
      
      IF m_saldo_apont < m_qtd_movto THEN
         LET m_msg = 'A quantidade a estornar é maior que a quantidade apontada.'      

  	     IF NOT pol1306_ins_erro() THEN
	    	    RETURN
	    	 END IF

         LET m_integrado = 3
  	 	   IF NOT pol1306_atu_man() THEN
	 	        RETURN
	 	     END IF
	 	     
	 	     CONTINUE FOREACH
	 	  END IF
        
      DECLARE cq_w_estorna CURSOR WITH HOLD FOR 
       SELECT seq_reg_mestre, 
              qtd_apont,
              qtd_produzida,
              qtd_convertida
         FROM w_estorna_304
        ORDER BY seq_reg_mestre
      
      FOREACH cq_w_estorna INTO 
             m_seq_reg_mestre, m_saldo_apont, m_qtd_produzida, m_qtd_convertida
      
         IF STATUS <> 0 THEN
            LET m_erro = STATUS
            LET m_msg = 'ERRO ',m_erro CLIPPED, ' LENDO TABELA W_ESTORNA_304'
            RETURN
         END IF 

         IF m_tip_movto = '2' THEN
            UPDATE man_item_produzido SET qtd_convertida = qtd_produzida
             WHERE empresa = p_cod_empresa AND seq_reg_mestre = m_seq_reg_mestre
         END IF
         
         IF NOT pol1306_le_dados() THEN
            RETURN
         END IF

         LET m_integrado = 3	
	 			 LET m_cod_status = 'C'
                  
         IF manr24_cria_w_apont_prod(0)  THEN 
 		        CALL man8246_cria_temp_fifo()
	          CALL man8237_cria_tables_man8237()  
            DELETE FROM w_parada
	          IF manr24_inclui_w_apont_prod(p_w_apont_prod.*,1) THEN # incluindo apontamento
			         IF m_tip_movto = '3' THEN
			            IF func007_estorna_apont() THEN
	 			             LET m_integrado = 2	 
	 			             LET m_cod_status = 'A'
	 			          END IF
			         ELSE			         
			            LET lr_tip_sucata.cod_empresa = p_w_apont_prod.cod_empresa
			            LET lr_tip_sucata.seq_reg_mestre = p_w_apont_prod.num_seq_registro
			            LET lr_tip_sucata.estorno_total = 'S'			            
			            LET lr_tip_sucata.item_trata_qea = 1
                  CALL man8232_carrega_sucata(lr_tip_sucata.*,1) RETURNING p_status
 	                IF manr27_processa_apontamento()  THEN #processando ESTORNO
	 			             LET m_integrado = 2	 
	 			             LET m_cod_status = 'A'
	 			          END IF 
	 	           END IF 
	 	        ELSE
	 	           LET m_txt_resumo = 'ERRO:',STATUS,'INCLUINDO TAB W_APONT_PROD'
	 	           INSERT INTO man_log_apo_prod(empresa,ordem_producao,texto_resumo)
	 	            VALUES(p_cod_empresa, m_num_ordem, m_txt_resumo) 
	 	        END IF
	 	     ELSE
            LET m_txt_resumo = 'ERRO:',STATUS,'CRIANDO TAB W_APONT_PROD'
            INSERT INTO man_log_apo_prod(empresa,ordem_producao,texto_resumo)
	 	         VALUES(p_cod_empresa, m_num_ordem, m_txt_resumo) 	 	     
	 	     END IF

	 	     IF NOT pol1306_atu_man() THEN
	 	        RETURN
	 	     END IF

         IF m_tip_movto = '2' THEN
           UPDATE man_item_produzido SET qtd_convertida = m_qtd_convertida
            WHERE empresa = p_cod_empresa 
              AND seq_reg_mestre = m_seq_reg_mestre
         END IF

         IF m_integrado = 3 THEN
            IF NOT pol1306_le_erros() THEN
	             RETURN
	          END IF			     
         ELSE
            IF m_tip_movto = '2' THEN
               IF NOT pol1306_corrige_apont() THEN
                  RETURN
               END IF
            END IF
	       END IF
	 	     
	 	     DELETE FROM w_apont_prod 
	 	           
         IF m_reaponta > 0 THEN
            IF NOT pol1306_ger_apont() THEN
               RETURN
            END IF
         END IF
            
         IF m_qtd_movto <= 0 OR m_integrado = 3 THEN
            EXIT FOREACH
         END IF
         
      END FOREACH
      	 	             	
   END FOREACH

    
END FUNCTION

#--------------------------#
FUNCTION pol1306_le_dados()#
#--------------------------#
   
   INITIALIZE p_w_apont_prod TO NULL
   
   SELECT
    man_apo_mestre.empresa,                   
    man_apo_mestre.seq_reg_mestre,
    man_apo_mestre.item_produzido,  
    man_apo_mestre.ordem_producao,
    man_apo_mestre.data_producao,
    man_apo_mestre.usu_apontamento,
    man_apo_mestre.secao_requisn,   
    man_tempo_producao.data_ini_producao,  
    man_tempo_producao.hor_ini_producao,  
    man_tempo_producao.dat_final_producao,  
    man_tempo_producao.hor_final_producao,  
    man_tempo_producao.turno_producao,
    man_apo_detalhe.roteiro_fabr,
    man_apo_detalhe.altern_roteiro,
    man_apo_detalhe.operacao,  
    man_apo_detalhe.sequencia_operacao,
    man_apo_detalhe.centro_trabalho,
    man_apo_detalhe.arranjo_fisico,
    man_apo_detalhe.ferramental,  
    man_apo_detalhe.atlz_ferr_min,      
    man_apo_detalhe.eqpto,  
    man_apo_detalhe.atualiza_eqpto_min,  
    man_apo_detalhe.operador,
    man_apo_detalhe.nome_programa,  
    man_item_produzido.lote_produzido,
    man_item_produzido.grade_1,
    man_item_produzido.grade_2,
    man_item_produzido.grade_3,
    man_item_produzido.grade_4,
    man_item_produzido.grade_5,
    man_item_produzido.local,
    man_item_produzido.sit_est_producao,
    man_item_produzido.qtd_produzida
    
   INTO p_w_apont_prod.cod_empresa,            
        p_w_apont_prod.num_seq_registro,  	 
        p_w_apont_prod.cod_item,        		 
        p_w_apont_prod.num_ordem,           
        p_w_apont_prod.dat_producao,     
        p_w_apont_prod.nom_usuario,   
        p_w_apont_prod.num_secao_requis,    
        p_w_apont_prod.dat_ini_prod,        
        p_w_apont_prod.hor_ini_periodo,     
        p_w_apont_prod.dat_fim_prod,        
        p_w_apont_prod.hor_fim_periodo,     
        p_w_apont_prod.cod_turno,           
 	      p_w_apont_prod.cod_roteiro,     
        p_w_apont_prod.num_altern,      
        p_w_apont_prod.cod_operacao,        
        p_w_apont_prod.num_seq_operac,      
        p_w_apont_prod.cod_cent_trab,       
        p_w_apont_prod.cod_arranjo,         
        p_w_apont_prod.cod_ferram,          
        p_w_apont_prod.ies_ferram_min,      
        p_w_apont_prod.cod_equip,           
        p_w_apont_prod.ies_equip_min,       
        p_w_apont_prod.num_operador,        
        p_w_apont_prod.num_programa,        
        p_w_apont_prod.num_lote,            
        p_w_apont_prod.cod_item_grade1,     
        p_w_apont_prod.cod_item_grade2,     
        p_w_apont_prod.cod_item_grade3,     
        p_w_apont_prod.cod_item_grade4,     
        p_w_apont_prod.cod_item_grade5,     
        p_w_apont_prod.cod_local,
        p_w_apont_prod.ies_sit_qtd,        
        p_w_apont_prod.qtd_boas_ant

    FROM man_apo_mestre, 
        man_tempo_producao,
        man_apo_detalhe,
        man_item_produzido 
   
   WHERE man_apo_mestre.empresa = p_cod_empresa
     AND man_apo_mestre.seq_reg_mestre = m_seq_reg_mestre
     AND man_tempo_producao.empresa = man_apo_mestre.empresa
     AND man_tempo_producao.seq_reg_mestre = man_apo_mestre.seq_reg_mestre
     AND man_apo_detalhe.empresa = man_apo_mestre.empresa  
     AND man_apo_detalhe.seq_reg_mestre = man_apo_mestre.seq_reg_mestre  
     AND man_item_produzido.empresa = man_apo_mestre.empresa
     AND man_item_produzido.seq_reg_mestre = man_apo_mestre.seq_reg_mestre
     AND man_item_produzido.tip_movto = 'N'
     
   IF STATUS <> 0 THEN
      LET m_erro = STATUS
      LET m_msg = 'ERRO ',m_erro CLIPPED, ' LENDO APONTAMENTO A ESTORNAR'
      RETURN FALSE
   END IF 
   
   SELECT cod_local_prod,
          cod_local_estoq,
          num_lote		
     INTO p_w_apont_prod.cod_local,
          p_w_apont_prod.cod_local_est,	
          p_w_apont_prod.num_lote	
     FROM ordens
    WHERE cod_empresa = p_cod_empresa
      AND num_ordem = m_num_ordem

   IF STATUS <> 0 THEN
      LET m_erro = STATUS
      LET m_msg = 'ERRO ',m_erro CLIPPED, ' LENDO DADOS DA TAB ORDENS'
      RETURN FALSE
   END IF 

   IF NOT pol1306_le_ord_oper() THEN
      RETURN FALSE
   END IF
   
   LET p_w_apont_prod.cod_tip_movto = 'E'
   LET m_reaponta = 0
      
   IF m_saldo_apont  <= m_qtd_movto THEN
      LET p_w_apont_prod.estorno_total = 'S' 
      LET p_w_apont_prod.qtd_boas = m_saldo_apont
      LET m_qtd_movto = m_qtd_movto - m_saldo_apont
   ELSE
      IF m_tip_movto = '2' THEN
         LET m_reaponta = m_saldo_apont - m_qtd_movto
         LET p_w_apont_prod.qtd_boas = m_saldo_apont
         LET p_w_apont_prod.estorno_total = 'S'
         LET m_qtd_movto = 0
      ELSE 
         LET p_w_apont_prod.estorno_total = 'N' 
         LET p_w_apont_prod.qtd_boas = m_qtd_movto
         LET m_qtd_movto = 0
      END IF
   END IF
   
   LET p_w_apont_prod.qtd_refug = 0			
   LET p_w_apont_prod.qtd_refug_ant = 0			
   LET p_w_apont_prod.ies_defeito = 0
   LET p_w_apont_prod.ies_parada = 0
   LET p_w_apont_prod.ies_apontamento	= '1'	
   LET p_w_apont_prod.abre_transacao = 1
   LET p_w_apont_prod.modo_exibicao_msg	= 0
   LET p_w_apont_prod.endereco = ' '
   LET p_w_apont_prod.identif_estoque	= NULL
   LET p_w_apont_prod.sku	= NULL 
   LET p_w_apont_prod.num_docum = m_num_docum
   LET p_w_apont_prod.observacao = '  '
   LET p_w_apont_prod.finaliza_operacao = 'N'
   LET p_w_apont_prod.tip_servico = ' '

   IF m_tip_movto = '1' THEN
      LET p_w_apont_prod.ies_sucata = 0
   ELSE
      LET p_w_apont_prod.ies_sucata = 1
		  LET p_w_apont_prod.qtd_boas = 0     
		  LET p_w_apont_prod.qtd_boas_ant = 0 
		  LET p_w_apont_prod.ies_sit_qtd =  ' '
		  LET p_w_apont_prod.cod_roteiro = '               '      
		  LET p_w_apont_prod.num_altern = 0
		  LET p_w_apont_prod.observacao =  ' '
		  LET p_w_apont_prod.num_lote = NULL
		  LET p_w_apont_prod.cod_local_est = NULL
   END IF
   
   RETURN TRUE

END FUNCTION

#-----------------------------#
FUNCTION pol1306_le_ord_oper()#
#-----------------------------#

   SELECT seq_processo
     INTO m_seq_processo
     FROM ord_oper
    WHERE cod_empresa = p_cod_empresa
      AND num_ordem = m_num_ordem
      AND num_seq_operac = p_w_apont_prod.num_seq_operac

   IF STATUS <> 0 THEN
      LET m_erro = STATUS
      LET m_msg = 'ERRO ',m_erro CLIPPED, ' LENDO DADOS DA TAB ORD_OPER'
      RETURN FALSE
   END IF 
   
   RETURN TRUE

END FUNCTION

#-------------------------------#
FUNCTION pol1306_corrige_apont()#
#-------------------------------#
   
   UPDATE ordens 
      SET qtd_sucata = qtd_sucata + m_qtd_produzida - m_qtd_convertida
    WHERE cod_empresa = p_cod_empresa 
      AND num_ordem = m_num_ordem

   IF STATUS <> 0 THEN
      LET m_erro = STATUS
      LET m_msg = 'ERRO ',m_erro CLIPPED, ' CORRIGINDO DADOS DA TAB ORDENS'
      RETURN FALSE
   END IF 
   
   UPDATE ord_oper 
      SET qtd_sucata = qtd_sucata + m_qtd_produzida - m_qtd_convertida
    WHERE cod_empresa = p_cod_empresa 
      AND num_ordem = m_num_ordem
      AND num_seq_operac = m_num_seq_operac

   IF STATUS <> 0 THEN
      LET m_erro = STATUS
      LET m_msg = 'ERRO ',m_erro CLIPPED, ' CORRIGINDO DADOS DA TAB ORD_OPER'
      RETURN FALSE
   END IF 
   
   RETURN TRUE

END FUNCTION
   
#---------------------------#
FUNCTION pol1306_ger_apont()#
#---------------------------#
   
   DEFINE l_man     RECORD
    cod_empresa char(2),
    id_registro integer ,
    num_ordem integer,
    num_pedido integer,
    num_seq_pedido integer,
    cod_item char(15),
    cod_roteiro char(15),
    num_rot_alt decimal(2,0),
    num_lote char(15),
    dat_inicial datetime year to day,
    dat_final datetime year to day,
    cod_recur char(5),
    cod_operac char(5),
    num_seq_operac decimal(3,0),
    oper_final char(1),
    cod_cent_trab char(5),
    cod_cent_cust decimal(4,0),
    cod_unid_prod char(5),
    cod_arranjo char(5),
    qtd_refugo decimal(10,3),
    qtd_sucata decimal(10,3),
    qtd_boas decimal(10,3),
    comprimento integer,
    largura integer,
    altura integer,
    diametro integer,
    tip_apon char(1),
    tip_operacao char(1),
    cod_local_prod char(10),
    cod_local_est char(10),
    qtd_hor decimal(11,7),
    matricula char(8),
    cod_turno char(1),
    hor_inicial char(5),
    hor_final char(5),
    unid_funcional char(10),
    dat_atualiz datetime year to second,
    ies_terminado char(1),
    cod_eqpto char(15),
    cod_ferramenta char(15),
    integr_min char(1),
    nom_prog char(8),
    nom_usuario char(8),
    cod_status char(1),
    num_processo integer,
    num_proc_ant integer,
    num_proc_dep integer,
    num_transac integer,
    mensagem char(210),
    dat_process datetime year to second,
    id_apont integer,
    id_tempo integer,
    integrado integer,
    den_erro char(500),
    dat_integra char(20),
    usuario char(8),
    tip_integra char(1),
    concluido char(1),
    num_docum char(15),
    qtd_movto decimal(10,3),
    tip_movto char(1),
    qtd_tempo integer,
    dat_criacao datetime year to second
   END RECORD
   
   SELECT *
     INTO l_man.*
    FROM man_apont_304
   WHERE cod_empresa = p_cod_empresa
     AND id_registro = g_id_man_apont
       
   IF STATUS <> 0 THEN
      LET m_erro = STATUS
      LET m_msg = 'ERRO ',m_erro CLIPPED, ' LENDO DADOS DA TAB MAN_APONT_304'
      RETURN FALSE
   END IF 
   
   LET l_man.id_registro = 0
   LET l_man.integrado = 1
   LET l_man.tip_integra = 'A'
   LET l_man.qtd_movto = m_reaponta
   LET l_man.dat_criacao = CURRENT

   INSERT INTO man_apont_304 VALUES(l_man.*)
   
   IF STATUS <> 0 THEN
      LET m_erro = STATUS
      LET m_msg = 'ERRO ',m_erro CLIPPED, ' INSERINDO DADOS NA TAB MAN_APONT_304'
      RETURN FALSE
   END IF 
   
   LET m_reprocessa = TRUE
      
   RETURN TRUE

END FUNCTION

#---------------------------#
 FUNCTION pol1306_w_parada()
#---------------------------#
	
	DROP TABLE w_parada

	CREATE TEMP TABLE w_parada (
				cod_parada            CHAR(03),
				dat_ini_parada   			DATE,
				dat_fim_parada 				DATE,
				hor_ini_periodo 			DATETIME HOUR TO MINUTE,
				hor_fim_periodo 			DATETIME HOUR TO MINUTE,
				hor_tot_periodo 			DECIMAL(7,2)
		)

   IF STATUS <> 0 THEN
      LET m_erro = STATUS
      LET m_msg = 'ERRO ',m_erro CLIPPED, ' CRIANDO TABELA W_PARADA '
      RETURN FALSE
   END IF 

   RETURN TRUE

END FUNCTION 

#----------------------------#
FUNCTION pol1306_le_paradas()#
#----------------------------#

   DEFINE l_ordem             INTEGER,
          l_operacao          CHAR(05),
          l_dat_ini           DATETIME YEAR TO MINUTE,
          l_dat_fim           DATETIME YEAR TO MINUTE,
          l_count             INTEGER,
          l_dat_char          CHAR(20)

   DEFINE  l_w_parada RECORD
				cod_parada            CHAR(03),
				dat_ini_parada   			DATE,
				dat_fim_parada 				DATE,
				hor_ini_periodo 			DATETIME HOUR TO MINUTE,
				hor_fim_periodo 			DATETIME HOUR TO MINUTE,
				hor_tot_periodo 			DECIMAL(7,2)
   END RECORD 
   
   LET l_dat_char = EXTEND(p_w_apont_prod.dat_ini_prod, YEAR TO DAY),' ', 
          p_w_apont_prod.hor_ini_periodo
      
   LET l_dat_ini = l_dat_char
   
   LET l_dat_char = EXTEND(p_w_apont_prod.dat_fim_prod, YEAR TO DAY),' ',
          p_w_apont_prod.hor_fim_periodo
   
   LET l_dat_fim = l_dat_char
   
   DELETE FROM w_parada   
   
   DECLARE cq_parada CURSOR FOR
    SELECT cod_parada,
           DATE(dat_ini_parada),
           DATE(dat_fim_parada),
           EXTEND(dat_ini_parada, YEAR TO MINUTE),
           EXTEND(dat_fim_parada, YEAR TO MINUTE)
      FROM man_parada_912
     WHERE cod_empresa = p_cod_empresa
       AND num_ordem = m_num_ordem
       AND cod_operac = m_cod_operac
       AND dat_ini_parada >= l_dat_ini
       AND dat_fim_parada <= l_dat_fim
      
   FOREACH cq_parada INTO 
        l_w_parada.cod_parada,       
        l_w_parada.dat_ini_parada,   
        l_w_parada.dat_fim_parada, 	
        l_w_parada.hor_ini_periodo,  
        l_w_parada.hor_fim_periodo  

      IF STATUS <> 0 THEN
         LET m_erro = STATUS
         LET m_msg = 'ERRO ',m_erro CLIPPED, ' LENDO TABELA MAN_PARADA_912 '
         RETURN FALSE
      END IF 
      
      CALL pol1306_calc_hora(
             l_w_parada.hor_ini_periodo, l_w_parada.hor_fim_periodo)
      
      LET l_w_parada.hor_tot_periodo = m_qtd_hor
      
      INSERT INTO w_parada
       VALUES(l_w_parada.*)
       
      IF STATUS <> 0 THEN
         LET m_erro = STATUS
         LET m_msg = 'ERRO ',m_erro CLIPPED, ' INSERINDO DADOS NA TABELA W_PARADA '
         RETURN FALSE
      END IF 
      
   END FOREACH
   
   RETURN TRUE

END FUNCTION

#-----------------------------------------------#
FUNCTION pol1306_calc_hora(l_hor_ini, l_hor_fim)#
#-----------------------------------------------#

   DEFINE l_hor_ini     datetime hour to minute, 
          l_hor_fim     datetime hour to minute

   DEFINE l_hor_prod           CHAR(06),
          l_qtd_segundo        INTEGER,
          l_min, l_hor         INTEGER
          
     
	   IF l_hor_ini > l_hor_fim THEN
	      LET l_hor_prod = ('24:00' - (l_hor_ini - l_hor_fim))
	   ELSE
	      LET l_hor_prod = (l_hor_fim - l_hor_ini) 
	   END IF
	   	   
	   LET l_hor = l_hor_prod[2,3]
	   LET l_min = l_hor_prod[5,6]
	   
	   LET l_qtd_segundo = (l_hor * 3600) + (l_min * 60) 
	
	   LET m_qtd_hor = l_qtd_segundo / 3600


END FUNCTION

#-----------------------------------------#
FUNCTION pol1306_acerta_turno(l_cod_turno)#
#-----------------------------------------#

   DEFINE l_hor_ini           CHAR(05),
          l_hor_fim           CHAR(05),
          l_ini_s_ponto       CHAR(04),
          l_fim_s_ponto       CHAR(04),
          l_cod_turno         DECIMAL(3,0),
          l_hor_ini_normal    CHAR(04),
          l_hor_fim_normal    CHAR(04),
          l_dif_periodo       INTEGER

   LET l_hor_ini = p_w_apont_prod.hor_ini_periodo
   LET l_hor_fim = p_w_apont_prod.hor_fim_periodo
   LET l_ini_s_ponto = l_hor_ini[1,2],l_hor_ini[4,5]
   LET l_fim_s_ponto = l_hor_fim[1,2],l_hor_fim[4,5]
   
   SELECT hor_ini_normal,
          hor_fim_normal
    INTO l_hor_ini_normal,
         l_hor_fim_normal
    FROM turno
    WHERE cod_empresa = p_cod_empresa
      AND cod_turno = l_cod_turno

   IF STATUS <> 0 THEN
      LET m_erro = STATUS
      LET m_msg = 'ERRO ',m_erro CLIPPED, ' LENDO PERIODO DA TABELA TURNO '
      RETURN FALSE
   END IF
   
   IF l_ini_s_ponto < l_hor_ini_normal THEN
      LET l_dif_periodo = l_hor_ini_normal - l_ini_s_ponto
      IF l_dif_periodo > m_corte_periodo THEN
         LET m_msg = ' - Periodo produtivo fora do intervalo do turno ', l_cod_turno
	    	 IF NOT pol1306_ins_erro() THEN
	    	    RETURN FALSE
	    	 END IF
         RETURN TRUE
      END IF
      LET l_hor_ini = l_hor_ini_normal[1,2],':',l_hor_ini_normal[3,4]
      LET p_w_apont_prod.hor_ini_periodo = l_hor_ini
   END IF
   
   IF l_fim_s_ponto > l_hor_fim_normal THEN
      LET l_dif_periodo = l_fim_s_ponto - l_hor_fim_normal
      IF l_dif_periodo > m_corte_periodo THEN
         LET m_msg = ' - Periodo produtivo fora do intervalo do turno ', l_cod_turno
	    	 IF NOT pol1306_ins_erro() THEN
	    	    RETURN FALSE
	    	 END IF
         RETURN TRUE
      END IF
      LET l_hor_fim = l_hor_fim_normal[1,2],':',l_hor_fim_normal[3,4]
      LET p_w_apont_prod.hor_fim_periodo = l_hor_fim
   END IF   
   
   RETURN TRUE
   
END FUNCTION
